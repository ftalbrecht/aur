# $Id$
# Maintainer: Felix Schindler <felix at schindlerfaimly dot de>
# Contributor: Allan McRae <allan@archlinux.org>

# toolchain build order: linux-api-headers->glibc->binutils->gcc->binutils->glibc
# NOTE: libtool requires rebuilt with each new gcc version

pkgname=gcc48
_pkgver_minor=4.8
pkgver=${_pkgver_minor}.2
pkgrel=2
pkgdesc="The GNU Compiler Collection - C and C++ frontends (4.8.x)"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL' 'custom')
url="http://gcc.gnu.org"
depends=('binutils>=2.24' 'libmpc' 'cloog')
makedepends=('binutils>=2.24' 'libmpc' 'cloog' 'gcc-ada' 'doxygen')
checkdepends=('dejagnu' 'inetutils')
options=('!emptydirs' 'staticlibs' '!libtool')
source=(ftp://gcc.gnu.org/pub/gcc/releases/gcc-${pkgver}/gcc-${pkgver}.tar.bz2
        gcc-4.8-filename-output.patch
        gcc-4.8-lambda-ICE.patch)
md5sums=('a3d7d63b9cb6b6ea049469a0c4a43c9d'
         '40cb437805e2f7a006aa0d0c3098ab0f'
         '6eb6e080dbf7bc6825f53a0aaa6c4ef9')

_basedir=gcc-${pkgver}

_libdir="usr/lib/gcc/$CHOST/$pkgver"

build() {
  # prepare
  cd ${srcdir}/${_basedir}

  # Do not run fixincludes
  sed -i 's@\./fixinc\.sh@-c true@' gcc/Makefile.in

  # Arch Linux installs x86_64 libraries /lib
  [[ $CARCH == "x86_64" ]] && sed -i '/m64=/s/lib64/lib/' gcc/config/i386/t-linux64

  echo ${pkgver} > gcc/BASE-VER

  # hack! - some configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" {libiberty,gcc}/configure

  # http://gcc.gnu.org/bugzilla/show_bug.cgi?id=57653
  patch -p0 -i ${srcdir}/gcc-4.8-filename-output.patch
  
  # http://gcc.gnu.org/bugzilla//show_bug.cgi?id=56710 - commit 3d1f8279
  patch -p1 -i ${srcdir}/gcc-4.8-lambda-ICE.patch

  # installing libiberty headers is broken
  # http://gcc.gnu.org/bugzilla/show_bug.cgi?id=56780#c6
  sed -i 's#@target_header_dir@#libiberty#' libiberty/Makefile.in

  mkdir ${srcdir}/gcc-build

  # build
  cd ${srcdir}/gcc-build

  # Doesn't like FORTIFY_SOURCE
  CPPFLAGS=${CPPFLAGS//-D_FORTIFY_SOURCE=?/}
  export CPPFLAGS

  # using -pipe causes spurious test-suite failures
  # http://gcc.gnu.org/bugzilla/show_bug.cgi?id=48565
  CFLAGS=${CFLAGS/-pipe/}
  CXXFLAGS=${CXXFLAGS/-pipe/}

  ${srcdir}/${_basedir}/configure --prefix=/usr \
      --libdir=/usr/lib --libexecdir=/usr/lib \
      --mandir=/usr/share/man --infodir=/usr/share/info \
      --with-bugurl=https://bugs.archlinux.org/ \
      --enable-languages=c,c++,fortran \
      --enable-shared --enable-threads=posix \
      --with-system-zlib --enable-__cxa_atexit \
      --disable-libunwind-exceptions --enable-clocale=gnu \
      --disable-libstdcxx-pch --disable-libssp \
      --enable-gnu-unique-object --enable-linker-build-id \
      --enable-cloog-backend=isl --disable-cloog-version-check \
      --enable-lto --enable-plugin \
      --enable-gold --enable-ld=default --with-plugin-ld=ld.gold \
      --with-linker-hash-style=gnu \
      --disable-multilib --disable-werror \
      --program-suffix=-${pkgver} --enable-version-specific-runtime-libs \
      --enable-checking=release
  make
  
  # make documentation
  make -C $CHOST/libstdc++-v3/doc doc-man-doxygen
}

package()
{
  cd ${srcdir}/gcc-build

  make -C gcc DESTDIR=${pkgdir} install-driver install-cpp install-gcc-ar \
    c++.install-common install-headers install-plugin install-lto-wrapper

  install -m755 gcc/gcov $pkgdir/usr/bin/gcov-${pkgver}
  install -m755 -t $pkgdir/${_libdir}/ gcc/{cc1,cc1plus,collect2,lto1}

  make DESTDIR=${pkgdir} install-fixincludes
  make -C gcc DESTDIR=${pkgdir} install-mkheaders
  make -C lto-plugin DESTDIR=${pkgdir} install

  make -C $CHOST/libgomp DESTDIR=${pkgdir} install-nodist_toolexeclibHEADERS \
    install-nodist_libsubincludeHEADERS
  make -C $CHOST/libitm DESTDIR=${pkgdir} install-nodist_toolexeclibHEADERS
  make -C $CHOST/libmudflap DESTDIR=${pkgdir} install-nobase_libsubincludeHEADERS
  make -C $CHOST/libquadmath DESTDIR=${pkgdir} install-nodist_libsubincludeHEADERS
  make -C $CHOST/libsanitizer/asan DESTDIR=${pkgdir} install-nodist_toolexeclibHEADERS

  make -C gcc DESTDIR=${pkgdir} install-man install-info

  make -C libcpp DESTDIR=${pkgdir} install
  make -C gcc DESTDIR=${pkgdir} install-po

  rm -r ${pkgdir}/usr/share/info
  rm -r ${pkgdir}/usr/share/man/man7
  rm -r ${pkgdir}/usr/share/locale
  rm -r ${pkgdir}/usr/include

  # Install Runtime Library Exception
  install -d ${pkgdir}/usr/share/licenses/${pkgname}
  cp ${srcdir}/gcc-${pkgver}/COPYING.RUNTIME ${pkgdir}/usr/share/licenses/${pkgname}/

  # create symlinks
  cd ${pkgdir}/usr/bin
  for ii in c++ cpp g++ gcc gcc-ar gcc-nm gcc-ranlib; do
    ln -s ${ii}-${pkgver} ${ii}-${_pkgver_minor}
    ln -s ${CHOST}-${ii}-${pkgver} ${CHOST}-${ii}-${_pkgver_minor}
  done
  ln -s gcov-${pkgver} gcov-${_pkgver_minor}
}
