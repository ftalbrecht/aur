# Maintainer: Christoph Haag <haagch@studi,informatik.uni-stuttgart.de>
# Contributor: WorMzy Tykashi <wormzy.tykashi@gmail.com>
pkgname=steamos-compositor
pkgver=1.17.3
pkgrel=1
pkgdesc="Compositor used by SteamOS \"based on xcompmgr by Keith Packard et al.\""
arch=('i686' 'x86_64')
url="http://repo.steampowered.com/steamos/pool/main/s/steamos-compositor/"
license=('GPL')
depends=('libxfixes' 'libxext' 'libxcomposite' 'libxdamage' 'libxrender' 'libxxf86vm' 'sdl_image' 'libgl')
makedepends=('mesa' 'libxt' 'libxpm') # 'openmotif') # openmotif was open sourced some time ago and is more complete than lesstif. Anyway, it isn't referenced anywhere in the source or makefiles
source=("http://repo.steampowered.com/steamos/pool/main/s/steamos-compositor/steamos-compositor_${pkgver}.tar.gz")

prepare() {
  cd "$srcdir/$pkgname"
  rm -rf autom4te.cache aclocal.m4 configure
  autoreconf -i
}

build() {
  export LIBS="-lXm -lXext -lXt -lXpm -lXdamage -lXfixes"
  cd "$srcdir/$pkgname"
  ./configure --prefix=/usr
  make
}

check() {
  cd "$srcdir/$pkgname"
  make -k check
}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR="$pkgdir/" install
}

md5sums=('4fd34d1bc9bb20c4ae2ad724363feed9')
