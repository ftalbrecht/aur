# Maintainer: Felix Schindler
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Imanol Celaya <ornitorrincos@archlinux-es.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Dan Vratil <progdan@progdansoft.com>
# Contributor: thotypous <matiasΘarchlinux-br·org>
# Contributor: delor <bartekpiech gmail com>
#
# This PKGBUILD is maintained at hppts://github.com/ftalbrecht/aur

pkgname=qtcreator28
pkgver=2.8.1
_pkgver=2.8.1
pkgrel=2
pkgdesc='Lightweight, cross-platform integrated development environment (2.8.x)'
arch=('i686' 'x86_64')
url='http://qt-project.org'
license=('LGPL')
depends=('qt51-quick1' 'qt51-tools')
makedepends=('git' 'mesa' 'qt51-base' 'qt51-jsbackend')
options=('docs')
optdepends=('qt51-doc: for the integrated Qt documentation'
            'gdb: for the debugger'
            'cmake: for cmake project support'
            'openssh-askpass: for ssh support'
            'git: for git support'
            'mercurial: for mercurial support'
            'bzr: for bazaar support'
            'valgrind: for analyze support')
install=qtcreator28.install
source=("git+https://gitorious.org/qt-creator/qt-creator.git#tag=v${_pkgver}"
        "git+https://gitorious.org/qt-labs/qbs.git"
        'qtcreator28.desktop')
md5sums=('SKIP'
         'SKIP'
         'a94a19214f0e1de50e5c6cb04c212ca4')
_install_root="/opt/$pkgname"

prepare() {
  source /opt/qt51.PATH

  cd qt-creator
  git submodule init
  git config submodule.qbs.url $srcdir/qbs
  git submodule update
}

build() {
  source /opt/qt51.PATH

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  qmake ../qt-creator/qtcreator.pro
  make
  make docs -j1
}

package() {
  source /opt/qt51.PATH

  cd build

  make INSTALL_ROOT="${pkgdir}/$_install_root/usr/" install
  make INSTALL_ROOT="${pkgdir}/$_install_root/usr/" install_docs

  install -Dm644 ${srcdir}/qtcreator28.desktop ${pkgdir}/usr/share/applications/qtcreator28.desktop
  install -Dm644 ${srcdir}/qt-creator/LGPL_EXCEPTION.TXT ${pkgdir}/usr/share/licenses/$pkgname/LGPL_EXCEPTION.TXT
}
